/*
 * Copyright 2012-2017 Tobi29
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.gradle.kotlin.dsl.ProjectExtensionsKt as gskp

buildscript {
    def kotlinVersion = "1.2.21"
    def dokkaVersion = "0.9.15"

    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-dev"
        }
        maven {
            url "https://jitpack.io"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
    }
}

allprojects {
    def env = System.getenv()
    if (env.containsKey("VERSION")) {
        version = env["VERSION"]
    } else {
        version = findProperty("build.version")
    }
    group = "com.github.Tobi29.ScapesEngineBuild"

    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-dev"
        }
        maven {
            url "https://jitpack.io"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.4.1"
    distributionUrl =
            "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

ext {

    scapesEngineVersion = "0.0.10"
    scapesEngine = { module ->
        "com.github.Tobi29.ScapesEngine:$module:$scapesEngineVersion"
    }
    scapesEngineJVM = { module -> scapesEngine("${module}JVM") }

    shadowVersion = "2.0.1"
    shadowJVM = { module ->
        "com.github.jengelman.gradle.plugins:$module:$shadowVersion"
    }

    downloadTaskVersion = "3.3.0"
    downloadTaskJVM = { module ->
        "de.undercouch:$module:$downloadTaskVersion"
    }
}

apply from: "$rootDir/resources/scapesenginemodulekotlinjvmnotest.gradle"

group = "com.github.Tobi29"

dependencies {
    compile gskp.gradleKotlinDsl(project)
    compile rootProject.ext.scapesEngineJVM("STDEx")
    compile rootProject.ext.scapesEngineJVM("Base64")
    compile rootProject.ext.scapesEngineJVM("IOUtils")
    compile rootProject.ext.shadowJVM("shadow")
    compile rootProject.ext.downloadTaskJVM("gradle-download-task")
}
